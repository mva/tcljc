(ns tcljc.emitter.namespace
  (:require
   [tcljc.config :as cfg]
   [tcljc.wrong :as wrong]
   [tcljc.grammar :as grammar]
   [tcljc.reader :as rd]
   [tcljc.context :as ctx]
   [tcljc.expr :as x]
   [tcljc.exprfold :as xf]
   [tcljc.compiler :as compiler]
   [tcljc.publics :as publics]
   [tinyclj.string :as str])
  (:import
   (tcljc.reader IReader)
   (tcljc.context Ctx)
   (tcljc.expr BindingRef)
   (clojure.lang Symbol ISeq IPersistentMap IPersistentSet IPersistentVector
                 ExceptionInfo)
   (java.lang.invoke MethodHandles$Lookup)
   (java.net URL)
   (java.nio.file Path)
   (java.util.concurrent Future TimeoutException)))

(definterface IBuildEnv
  (rt-lookup ^MethodHandles$Lookup [])
  (resolve-rpath ^URL [^Path rpath]) ;returns nil on failure
  (forms-reader ^IReader [^URL rurl]))

(deftype Namespace [^IBuildEnv env
                    ^IPersistentMap provided-nmsp ;string -> Namespace
                    ^Ctx ctx
                    
                    ^IPersistentSet rpaths
                    ^IPersistentSet req-strs
                    ^Class capstone
                    ^IPersistentMap exported-known-ns
                    ^IPersistentVector bytesv]) ;emitted classes, or stays nil

(defn- mk-nmsp ^Namespace [^IBuildEnv env ^IPersistentMap provided-nmsp
                           ^Ctx ctx]
  (Namespace. env provided-nmsp ctx #{} #{} nil nil nil))

(defn mk-provided ^Namespace [^String ns-str ^IPersistentMap publics]
  (Namespace. nil nil nil #{} #{} nil {ns-str publics} nil))

(defn assoc-provided [provided-nmsp ^String ns-str ^IPersistentMap publics]
  (assoc provided-nmsp ns-str (mk-provided ns-str publics)))

(def ^:private compile-namespace-in-progress (mk-nmsp nil nil nil))

(defn- with-rpath ^Namespace [^Namespace nmsp ^Path rpath]
  (Namespace. (.env nmsp) (.provided-nmsp nmsp) (.ctx nmsp)
              (conj (.rpaths nmsp) rpath) (.req-strs nmsp)
              (.capstone nmsp) (.exported-known-ns nmsp) (.bytesv nmsp)))

(defn- with-require ^Namespace [^Namespace nmsp ^String ns-str]
  (Namespace. (.env nmsp) (.provided-nmsp nmsp) (.ctx nmsp)
              (.rpaths nmsp) (conj (.req-strs nmsp) ns-str)
              (.capstone nmsp) (.exported-known-ns nmsp) (.bytesv nmsp)))

(defn with-capstone ^Namespace [^Namespace nmsp ^Class capstone
                                ^IPersistentVector bytesv]
  (let [ctx (.ctx nmsp)
        publics (publics/extract ctx capstone)
        ;; do not re-export namespaces from `provided-nmsp`, because
        ;; this would cause obsoleted versions of the core namespace
        ;; to reach downstream namespaces like tinyclj.java.io
        kns (-> (reduce dissoc (.get-known-ns ctx) (keys (.provided-nmsp nmsp)))
                (assoc (.ns-str ctx) publics))]
    (Namespace. nil nil nil ;clear `env`, `provided-nmsp`, and `ctx`
                (.rpaths nmsp) (.req-strs nmsp) capstone kns bytesv)))

(defn- file-rpath ^Path [^Path parent-rpath ^String path-str]
  (let [s (str path-str cfg/source-suffix)]
    (if (str/starts-with? path-str "/")
      (cfg/path-of s)
      (.resolve (.getParent parent-rpath) s))))

(def marker-depth-first-path (Object.)) ;FIXME... (Object/identityObject)

(defn publics-of ^IPersistentMap [^Namespace nmsp ^String ns-str]
  (-> (.exported-known-ns nmsp)
      (get ns-str)
      (java.util.Objects/requireNonNull)))

(defn- nmsp-of ^Namespace [x ^String ns-str]
  (cond
    (identical? marker-depth-first-path x)
    (wrong/other (str "cyclic require via " (wrong/q (symbol ns-str))
                      " and this namespace"))
    
    (instance? Future x)
    (let [fut ^Future x]
      (try
        (.get fut cfg/wait-cyclic-ms java.util.concurrent.TimeUnit/MILLISECONDS)
        (catch java.util.concurrent.ExecutionException e ;unwrap
          (throw (or (.getCause e) ^Throwable e)))
        (catch TimeoutException e     ;may not be repeatable!
          (wrong/mk-e (str "Wait for require " (wrong/q (symbol ns-str))
                           " timed out.  Maybe a cyclic import?  "
                           "Use --deterministic for better feedback.")))))

    :else x))

(defn- put-required-globals ^void [^Ctx ctx publics-map require-map]
  (assert (identical? :require (:tag require-map)))
  (let [{:keys [ns-sym]} require-map
        req-str (name ns-sym)
        req-defs (get publics-map req-str)
        
        incl (:refer-include require-map)
        excl (:refer-exclude require-map)
        rename (or (:rename require-map) {})
        put-req (fn ^void [^Symbol nm ^BindingRef bref]
                  (let [nm-sym (get rename nm nm)
                        nm* (name nm-sym)]
                    (if (contains? (.get-globals ctx) nm*)
                      (wrong/other (str "required name already defined by this "
                                        "namespace: " (wrong/q nm-sym)))
                      (.put-global ctx nm* bref))))]
    (if (or (identical? :all incl) (some? excl))
      (if (some? excl)
        (let [excl-set (set excl)]
          (doseq [[nm-str def] req-defs
                  :when (string? nm-str)
                  :let [nm (symbol nm-str)]
                  :when (not (contains? excl-set nm))]
            (put-req nm def)))
        (doseq [[nm-str def] req-defs
                :when (string? nm-str)]
          (put-req (symbol nm-str) def)))
      (doseq [nm incl]
        (if-some [bref (get req-defs (name nm))]
          (put-req nm bref)
          (wrong/unresolved ns-sym nm))))))

(defn- put-ns-alias ^void [^Ctx ctx ^String ns-str ^Symbol opt-alias]
  (let [ns-alias (.get-ns-alias-map ctx)]
    (when (= ns-str (.ns-str ctx))
      (wrong/other (str "namespace cannot require itself: "
                        (wrong/q ns-str))))
    (when-some [prev (.valAt ns-alias ns-str)]
      (when (not= prev ns-str)
        (wrong/other (str "namespace alias already exists: "
                          (wrong/q prev)))))
    (.put-ns-alias ctx ns-str ns-str)
    (when (some? opt-alias)
      (let [alias (name opt-alias)]
        (when (some? (.valAt ns-alias alias))
          (wrong/other (str "namespace alias already exists: "
                            (wrong/q opt-alias))))
        (.put-ns-alias ctx alias ns-str)))))

(defn- add-namespace-deps ^void [^Ctx ctx publics-map deps]
  (doseq [dep deps]
    (case (:tag dep)
      :import (compiler/add-imports dep ctx)
      :require (do (put-ns-alias ctx (name (:ns-sym dep)) (:as dep))
                   (put-required-globals ctx publics-map dep)))))

(letfn [(ns-str ^String [^Namespace nmsp]
          (-> nmsp .ctx .ns-str))
        (core-lib? ^boolean [^Namespace nmsp]
          (cfg/core-lib? (ns-str nmsp)))
        (core-provided-nmsp ^IPersistentMap [^Namespace nmsp ^boolean require?]
          (if (and require? (core-lib? nmsp))
            (let [force-x (ctx/send-msg (-> nmsp .ctx .emit)
                                        ctx/req-flush-namespace-segment nil)
                  globals (-> (-> nmsp .ctx .get-globals)
                              (assoc cfg/key-force-clinit force-x))]
              (assoc-provided (.provided-nmsp nmsp) cfg/core-lib globals))
            (.provided-nmsp nmsp)))
        (conj-force-clinit [statmv globals]
          (conj statmv (-> (get globals cfg/key-force-clinit)
                           (java.util.Objects/requireNonNull))))
        (add-require-deps ^Namespace [^Namespace nmsp deps]
          (let [req-strs (grammar/required-ns-strs deps)
                n (alength req-strs)
                a (new objects n)
                apublics (new "[Lclojure.lang.IPersistentMap;" n)
                provide-ns (core-provided-nmsp nmsp (some? deps))]
            ;; schedule namespaces in parallel
            (dotimes [i (alength req-strs)]
              (let [req-str (aget req-strs i)]
                (aset a i (or (get (.provided-nmsp nmsp) req-str)
                              (-> (.require-namespace (.ctx nmsp))
                                  (.schedule req-str provide-ns))))))
            ;; wait for and pick up namespace outcomes
            (dotimes [i n]              ;avoid stack frame spamming
              (let [ns-str (aget req-strs i)]
                (aset apublics i (-> (nmsp-of (aget a i) ns-str)
                                     (publics-of ns-str)))))
            ;; collect all "require"d globals and then add any new
            ;; globals to this namespace
            (loop [publics-map {}, i 0]
              (if (= i n)
                (doto (.ctx nmsp)
                  (.merge-into-known-ns publics-map)
                  (add-namespace-deps publics-map deps))
                (let [ns-str (aget req-strs i)
                      ekns (.exported-known-ns (nmsp-of (aget a i) ns-str))]
                  (recur (merge publics-map ekns) (inc i)))))
            ;; ensure that all required namespace classes are
            ;; initialized at this point
            (ctx/send-msg (-> nmsp .ctx .emit) ctx/msg-emit-tlf
                          (-> (reduce conj-force-clinit [] apublics)
                              (xf/progn-expr x/expr-empty)))
            (reduce with-require nmsp req-strs)))
        (top-level-list-form ^Namespace [^Namespace nmsp ^Path rpath ^ISeq form]
          (let [op (first form)]
            (case op
              ns
              (let [ns-str (ns-str nmsp)
                    [ns-sym & deps] (grammar/parse-ns-form form ns-str)]
                (add-require-deps nmsp deps))

              require
              (let [deps (grammar/parse-require-specs (rest form))]
                (add-require-deps nmsp deps))
           
              load
              (do (when (and (core-lib? nmsp) (= (fnext form) "core_print"))
                    ;; hack for dependency chain core_print.cljt
                    ;; -> MultiFn.java -> tinyclj.core fns
                    (ctx/send-msg (-> nmsp .ctx .emit)
                                  ctx/req-flush-namespace-segment nil))
                  (reduce
                   (fn ^Namespace [^Namespace nmsp ^String path-str]
                     (let [rpath (file-rpath rpath path-str)]
                       (if-some [rurl (.resolve-rpath (.env nmsp) rpath)]
                         (top-level-file nmsp rpath rurl)
                         (wrong/file rpath))))
                   nmsp (next form)))
           
              in-ns
              (let [[arg & argr] (grammar/strip-quotes (rest form))
                    ns-sym (symbol (ns-str nmsp))]
                (if (and (nil? argr) (= arg ns-sym))
                  nmsp
                  (wrong/other (str "in-ns argument does not match current "
                                    "namespace " (wrong/q ns-sym)))))
           
              #_else
              (do (compiler/top-level-form form (.ctx nmsp))
                  nmsp))))
        (top-level-file ^Namespace [^Namespace nmsp ^Path rpath ^URL rurl]
          ;; FIXME... use proper ns-alias map later
          (let [rdr (.forms-reader (.env nmsp) rurl)
                ns-alias {rd/this-namespace-key (ns-str nmsp)}]
            (try
              (loop [nmsp (with-rpath nmsp rpath)]
                (let [form (.read-1 rdr ns-alias ::eof)]
                  (cond
                    (identical? ::eof form) nmsp
                    (seq? form) (recur
                                 (wrong/with-list-context form
                                   (top-level-list-form nmsp rpath form)))
                    :else (do (compiler/top-level-form form (.ctx nmsp))
                              (recur nmsp)))))
              (catch ExceptionInfo e
                (throw (wrong/assoc-if-new e :file/resource-url rurl))))))]
  
  (defn compile-thread [^IBuildEnv env ^IPersistentMap provided-nmsp ^Ctx ctx]
    (ctx/push-nmsp-context (.lookup-in-pkg ctx) (.ns-str ctx))
    (try
      (-> (mk-nmsp env provided-nmsp ctx)
          (top-level-file (ctx/this-rpath ctx) (ctx/this-rurl ctx)))
      (finally
        (ctx/pop-nmsp-context (.lookup-in-pkg ctx))))))
