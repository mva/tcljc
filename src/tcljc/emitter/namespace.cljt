(ns tcljc.emitter.namespace
  (:require
   [tcljc.config :as cfg]
   [tcljc.wrong :as wrong]
   [tcljc.grammar :as grammar]
   [tcljc.reader :as rd]
   [tcljc.context :as ctx]
   [tcljc.expr :as x]
   [tcljc.exprfold :as xf]
   [tcljc.compiler :as compiler]
   [tinyclj.string :as str])
  (:import
   (tcljc.reader IReader)
   (tcljc.context Ctx)
   (tcljc.expr BindingRef)
   (clojure.lang Symbol ISeq IPersistentMap IPersistentSet IPersistentVector
                 ExceptionInfo)
   (java.lang.invoke MethodHandles$Lookup)
   (java.net URL)
   (java.nio.file Path)
   (java.util.concurrent Future TimeoutException)))

(definterface IBuildEnv
  (rt-lookup ^MethodHandles$Lookup [])
  (resolve-rpath ^URL [^Path rpath]) ;returns nil on failure
  (forms-reader ^IReader [^URL rurl]))

(deftype Namespace [^IBuildEnv env
                    ^IPersistentMap provided-ns
                    ^Ctx ctx
                    
                    ^IPersistentSet rpaths
                    ^IPersistentSet req-strs
                    ^Class capstone
                    bytesv])            ;bytes of emitted classes, or nil
(defn- mk-nmsp ^Namespace [^IBuildEnv env ^IPersistentMap provided-ns ^Ctx ctx]
  (Namespace. env provided-ns ctx #{} #{} nil nil))
(def ^:private compile-namespace-in-progress (mk-nmsp nil nil nil))
(defn- with-rpath ^Namespace [^Namespace nmsp ^Path rpath]
  (Namespace. (.env nmsp) (.provided-ns nmsp) (.ctx nmsp)
              (conj (.rpaths nmsp) rpath) (.req-strs nmsp)
              (.capstone nmsp) (.bytesv nmsp)))
(defn- with-require ^Namespace [^Namespace nmsp ^String ns-str]
  (Namespace. (.env nmsp) (.provided-ns nmsp) (.ctx nmsp)
              (.rpaths nmsp) (conj (.req-strs nmsp) ns-str)
              (.capstone nmsp) (.bytesv nmsp)))
(defn with-capstone ^Namespace [^Namespace nmsp ^Class capstone
                                ^IPersistentVector bytesv]
  (Namespace. (.env nmsp) (.provided-ns nmsp) (.ctx nmsp)
              (.rpaths nmsp) (.req-strs nmsp) capstone bytesv))

(defn- file-rpath ^Path [^Path parent-rpath ^String path-str]
  (let [s (str path-str cfg/source-suffix)]
    (if (str/starts-with? path-str "/")
      (cfg/path-of s)
      (.resolve (.getParent parent-rpath) s))))

(def marker-depth-first-path (Object.)) ;FIXME... (Object/identityObject)

(defn nmsp-globals ^IPersistentMap [^String ns-str x] 
  (cond
    (identical? marker-depth-first-path x)
    (wrong/other (str "cyclic require via " (wrong/q (symbol ns-str))
                      " and this namespace"))
    
    (instance? Future x)
    (let [fut ^Future x]
      (try
        (.get fut cfg/wait-cyclic-ms java.util.concurrent.TimeUnit/MILLISECONDS)
        (catch java.util.concurrent.ExecutionException e ;unwrap
          (throw (or (.getCause e) ^Throwable e)))
        (catch TimeoutException e       ;may not be repeatable!
          (wrong/mk-e (str "Wait for require " (wrong/q (symbol ns-str))
                           " timed out.  Maybe a cyclic import?  "
                           "Use --deterministic for better feedback.")))))

    :else x))

(defn- put-required-globals ^void [^Ctx ctx gmaps require-map]
  (assert (identical? :require (:tag require-map)))
  (let [{:keys [ns-sym]} require-map
        req-str (name ns-sym)
        req-defs (get gmaps req-str)
        
        incl (:refer-include require-map)
        excl (:refer-exclude require-map)
        rename (or (:rename require-map) {})
        put-req (fn ^void [^Symbol nm ^BindingRef bref]
                  (let [nm-sym (get rename nm nm)
                        nm* (name nm-sym)]
                    (if (contains? (.get-globals ctx) nm*)
                      (wrong/other (str "required name already defined by this "
                                        "namespace: " (wrong/q nm-sym)))
                      (.put-global ctx nm* bref))))]
    (if (or (identical? :all incl) (some? excl))
      (if (some? excl)
        (let [excl-set (set excl)]
          (doseq [[nm-str def] req-defs
                  :when (string? nm-str)
                  :let [nm (symbol nm-str)]
                  :when (not (contains? excl-set nm))]
            (put-req nm def)))
        (doseq [[nm-str def] req-defs
                :when (string? nm-str)]
          (put-req (symbol nm-str) def)))
      (doseq [nm incl]
        (if-some [bref (get req-defs (name nm))]
          (put-req nm bref)
          (wrong/unresolved ns-sym nm))))))

(defn- put-ns-alias ^void [^Ctx ctx ^String ns-str ^Symbol opt-alias]
  (let [ns-alias (.get-ns-alias-map ctx)]
    (when (= ns-str (.ns-str ctx))
      (wrong/other (str "namespace cannot require itself: "
                        (wrong/q ns-str))))
    (when-some [prev (.valAt ns-alias ns-str)]
      (when (not= prev ns-str)
        (wrong/other (str "namespace alias already exists: "
                          (wrong/q prev)))))
    (.put-ns-alias ctx ns-str ns-str)
    (when (some? opt-alias)
      (let [alias (name opt-alias)]
        (when (some? (.valAt ns-alias alias))
          (wrong/other (str "namespace alias already exists: "
                            (wrong/q opt-alias))))
        (.put-ns-alias ctx alias ns-str)))))

(defn- add-namespace-deps ^void [^Ctx ctx gmaps deps]
  (doseq [dep deps]
    (case (:tag dep)
      :import (assert false) #_(parser/add-imports ns dep)
      :require (do (put-ns-alias ctx (name (:ns-sym dep)) (:as dep))
                   (put-required-globals ctx gmaps dep)))))

(letfn [(ns-str ^String [^Namespace nmsp]
          (-> nmsp .ctx .ns-str))
        (core-provided-ns ^IPersistentMap [^Namespace nmsp]
          (if (cfg/core-lib? (ns-str nmsp))
            ;; FIXME... flush namespace classes and provide actual
            ;; globals
            (let [globals {cfg/key-force-clinit x/expr-empty}]
              (assoc (.provided-ns nmsp) cfg/core-lib globals))
            (.provided-ns nmsp)))
        (conj-force-clinit [statmv globals]
          (conj statmv (-> (get globals cfg/key-force-clinit)
                           (java.util.Objects/requireNonNull))))
        (add-require-deps ^Namespace [^Namespace nmsp deps]
          (let [req-strs (grammar/required-ns-strs deps)
                n (alength req-strs)
                a (new objects n)
                anmsp (new "[Lclojure.lang.IPersistentMap;" n)]
            ;; schedule namespaces in parallel
            (dotimes [i (alength req-strs)]
              (let [req-str (aget req-strs i)]
                (aset a i
                      (or (get (.provided-ns nmsp) req-str)
                          (-> (.require-namespace (.ctx nmsp))
                              (.schedule req-str (core-provided-ns nmsp)))))))
            ;; wait for and pick up namespace outcomes
            (dotimes [i n]              ;avoid stack frame spamming
              (aset anmsp i (nmsp-globals (aget req-strs i) (aget a i))))
            ;; collect all "require"d globals and then add any new
            ;; globals to this namespace
            (loop [gmaps {}, i 0]
              (if (= i n)
                (add-namespace-deps (.ctx nmsp) gmaps deps)
                (recur (assoc gmaps (aget req-strs i) (aget anmsp i))
                       (inc i))))
            ;; ensure that all required namespace classes are
            ;; initialized at this point
            (ctx/send-msg (-> nmsp .ctx .emit) ctx/msg-emit-tlf
                          (-> (reduce conj-force-clinit [] anmsp)
                              (xf/progn-expr x/expr-empty)))
            (reduce with-require nmsp req-strs)))
        (top-level-list-form ^Namespace [^Namespace nmsp ^Path rpath ^ISeq form]
          (let [op (first form)]
            (case op
              ns
              (let [ns-str (ns-str nmsp)
                    [ns-sym & deps] (grammar/parse-ns-form form ns-str)]
                (add-require-deps nmsp deps))

              require
              (let [deps (grammar/parse-require-specs (rest form))]
                (add-require-deps nmsp deps))
           
              load
              ;; (cond-> ns
              ;;   (and (core-lib? ns) (= (fnext form) "core_print"))
              ;;   ;; hack for dependency chain core_print.cljt
              ;;   ;; -> MultiFn.java -> tinyclj.core fns
              ;;   (flush-namespace-segment))
              (reduce
               (fn ^Namespace [^Namespace nmsp ^String path-str]
                 (let [rpath (file-rpath rpath path-str)]
                   (if-some [rurl (.resolve-rpath (.env nmsp) rpath)]
                     (top-level-file nmsp rpath rurl)
                     (wrong/file rpath))))
               nmsp (next form))
           
              in-ns
              (let [[arg & argr] (grammar/strip-quotes (rest form))
                    ns-sym (symbol (ns-str nmsp))]
                (if (and (nil? argr) (= arg ns-sym))
                  nmsp
                  (wrong/other (str "in-ns argument does not match current "
                                    "namespace " (wrong/q ns-sym)))))
           
              #_else
              (do (compiler/top-level-form form (.ctx nmsp))
                  nmsp))))
        (top-level-file ^Namespace [^Namespace nmsp ^Path rpath ^URL rurl]
          ;; FIXME... use proper ns-alias map later
          (let [rdr (.forms-reader (.env nmsp) rurl)
                ns-alias {rd/this-namespace-key (ns-str nmsp)}]
            (try
              (loop [nmsp (with-rpath nmsp rpath)]
                (let [form (.read-1 rdr ns-alias ::eof)]
                  (cond
                    (identical? ::eof form) nmsp
                    (seq? form) (recur
                                 (wrong/with-list-context form
                                   (top-level-list-form nmsp rpath form)))
                    :else (do (compiler/top-level-form form (.ctx nmsp))
                              (recur nmsp)))))
              (catch ExceptionInfo e
                (throw (wrong/assoc-if-new e :file/resource-url rurl))))))]
  
  (defn compile-thread [^IBuildEnv env ^IPersistentMap provided-ns ^Ctx ctx]
    (ctx/push-nmsp-context (.lookup-in-pkg ctx) (.ns-str ctx))
    (try
      (-> (mk-nmsp env provided-ns ctx)
          (top-level-file (ctx/this-rpath ctx) (ctx/this-rurl ctx)))
      (finally
        (ctx/pop-nmsp-context (.lookup-in-pkg ctx))))))
