;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.emitter.beachhead
  (:require [tcljc.classfile :as cf])
  (:import (jdk.classfile Classfile)
           (java.lang.constant ClassDesc MethodTypeDesc)
           (java.lang.invoke MethodHandles$Lookup MethodType)))

(let [nm-beachhead "__beachhead"
      cd-MethodHandles (ClassDesc/of "java.lang.invoke" "MethodHandles")
      cd-Lookup (ClassDesc/of "java.lang.invoke" "MethodHandles$Lookup")]
  
  (defn- beachhead ^bytes [^String pkg-name]
    (let [bhl-xb (cf/with-xbh [xb]
                   (-> (.invokestatic xb cd-MethodHandles "lookup"
                                      (MethodTypeDesc/of cd-Lookup))
                       (.areturn)))]
      (Classfile/build
       (ClassDesc/of pkg-name nm-beachhead)
       (cf/with-cbh [cb]
         (-> (cf/with-version cb)
             (.withFlags Classfile/ACC_PUBLIC)
             (.withMethodBody "beachheadLookup" (MethodTypeDesc/of cd-Lookup)
                              (bit-or Classfile/ACC_PUBLIC Classfile/ACC_STATIC)
                              bhl-xb))))))

  (defn mk-lookup-in-pkg ^MethodHandles$Lookup [^MethodHandles$Lookup rt-lookup
                                                ^ClassLoader dyn-ld
                                                ^String pkg-name]
    (let [bs (beachhead pkg-name)
          mt (MethodType/methodType MethodHandles$Lookup (.arrayType Byte/TYPE))
          mh (.findVirtual rt-lookup (class dyn-ld) "lookupInPackage" mt)]
      ^MethodHandles$Lookup (.invokeExact (.bindTo mh dyn-ld) bs))))
