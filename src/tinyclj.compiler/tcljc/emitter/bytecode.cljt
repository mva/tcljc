;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.emitter.bytecode
  (:require
   [tcljc.classfile :as cf]
   [tcljc.util :as util]
   [tcljc.config :as cfg]
   [tcljc.javabase :as jb]
   [tcljc.runtime :as rt]
   [tcljc.expr :as x]
   [tcljc.exprfold :as xf]
   [tcljc.context :as ctx]
   [tcljc.emitter.exprcode :as xc])
  (:import
   (tcljc.classfile ILdStInstr LdStMember)
   (tcljc.expr Expr Const Insn NewArray InvokeXref InvokeDynamic InvokePoly
               InvokeArity InvokeArrayClone Setter Progn Block Branch
               SplitJoin Guard Switch TryHandler TryCatchFinally LoopBody Recur
               Binding BindingRef AliasRef AssignDef VarRef
               FnForm ArityCode Arity FnPatch IEmitPlan IProvidedValue)
   (clojure.lang Symbol IPersistentVector IMeta)
   (jdk.classfile Classfile ClassfileBuilder ClassBuilder CodeBuilder
                  FieldBuilder Opcode Label
                  Annotation AnnotationElement AnnotationValue Signature)
   (jdk.classfile.instruction SwitchCase)
   (jdk.classfile.attribute ConstantValueAttribute SignatureAttribute
                            RuntimeVisibleAnnotationsAttribute)
   (java.lang.constant ConstantDesc ClassDesc DirectMethodHandleDesc$Kind
                       DirectMethodHandleDesc Constable DynamicCallSiteDesc)
   (java.lang.invoke TypeDescriptor$OfField)))

(def acc-static (bit-or Classfile/ACC_PUBLIC Classfile/ACC_STATIC Classfile/ACC_FINAL))
(def acc-virtual (bit-or Classfile/ACC_PUBLIC Classfile/ACC_FINAL))


(deftype ReadCounts [^int local ^int incr ^boolean other?])
(def ^:private rc-local (ReadCounts. 1 0 false))
(def ^:private rc-other (ReadCounts. 0 0 true))

(defn rc-iinc? ^boolean [^ReadCounts a]
  (and (not (.other? a)) (= (.local a) 1) (jb/in-short? (.incr a))))

(defn- rc-incr ^ReadCounts [^int n]
  (ReadCounts. 0 n false))
(defn- rc-add ^ReadCounts [^ReadCounts a ^ReadCounts b]
  (ReadCounts. (+ (.local a) (.local b)) (+ (.incr a) (.incr b))
               (or (.other? a) (.other? b))))
(defn- rc-neg ^ReadCounts [^ReadCounts a]
  (ReadCounts. (- (.local a)) (- (.incr a)) (.other? a)))

(defn- int-expr-counts ^ReadCounts [^BindingRef local ^Expr x]
  (cond
    (x/const-value? x)
    (let [c (.const-value x)]
      (if (instance? Integer c)
        (rc-incr ^int c)
        rc-other))
    
    (x/insn? x)
    (let [[^Expr a ^Expr b] (x/argv x)]
      (condp = (-> ^Insn x .instr .opcode)
        Opcode/IADD (rc-add (int-expr-counts local a)
                            (int-expr-counts local b))
        Opcode/ISUB (rc-add (int-expr-counts local a)
                            (rc-neg (int-expr-counts local b)))
        Opcode/INEG (rc-neg (int-expr-counts local a))
        #_else rc-other))
    
    (x/bref? x)
    (if (identical? local x) rc-local rc-other)

    :else rc-other))

;;; ------------------------------------------------------------------------

(deftype EmitState [^int arity-level
                    ^objects arity-closes-over
                    ^ILdStInstr ld-this
                    ^Label recur-target]
  IProvidedValue
  (apply [_ origin-bref]
    (if (= (x/b-level origin-bref) arity-level)
      (x/deref-load origin-bref)
      (if-some [bref (util/sim-get arity-closes-over origin-bref)]
        (do (assert (= (x/b-level bref) arity-level))
            (x/deref-load bref))
        ;; If the binding is not provided by this function and not
        ;; something this function closes over itself, then it must be
        ;; the external name of this function and can be emitted as
        ;; its local "this" value.
        (java.util.Objects/requireNonNull ld-this)))))

(defn- with-recur-target ^EmitState [^EmitState es ^Label recur-target]
  (EmitState. (.arity-level es) (.arity-closes-over es)
              (.ld-this es) recur-target))

(defn- line-number ^CodeBuilder [^CodeBuilder xb ^Expr x]
  (when cfg/enable-line-numbers
    (let [form (x/form x)]
      (when (instance? IMeta form)
        (when-some [m (.meta ^IMeta form)]
          (when-some [line (.valAt m :line nil)]
            (.lineNumber xb ^int line))))))
  xb)

;;; CAUTION: This function must be free of side effects, because the
;;; Classfile API can and will call the handlers that invoke `expr-insns`
;;; multiple times.
(defn- expr-insns ^CodeBuilder [^CodeBuilder xb ^EmitState es ^Expr x]
  (letfn [(exprv-insns ^CodeBuilder [^CodeBuilder xb ^IPersistentVector exprv]
            (dotimes [i (count exprv)]
              (expr-insns xb es (nth exprv i)))
            xb)
          (argv-insns ^CodeBuilder [^CodeBuilder xb]
            (exprv-insns xb (x/argv x)))
          (expr-handler ^java.util.function.Consumer [^Expr x]
            (cf/with-xbh [xb] (expr-insns xb es x)))
          (exprv-handler ^java.util.function.Consumer [^IPersistentVector exprv]
            (cf/with-xbh [xb] (exprv-insns xb exprv)))
          
          (deref-var ^CodeBuilder [^CodeBuilder xb ^Class tp]
            (cond-> (cf/invoke xb rt/mhd-Var-deref)
              (not (jb/object? tp))
              (.typeCheckInstruction Opcode/CHECKCAST (rt/cd-of tp))))
          (binding-ref ^CodeBuilder [^CodeBuilder xb ^BindingRef bref]
            ;; argument list of a binding can refer to a macro or
            ;; primitive function
            (if (x/b-value? bref)
              (cond-> (cf/ld-insn xb (x/deref-load bref))
                (x/b-def-var? bref) (deref-var (x/type x)))
              xb))
          
          (bind-local-insn
            (^CodeBuilder [^CodeBuilder xb ^Binding b ^Expr v]
             (bind-local-insn xb b v (x/type (.bref b))))
            (^CodeBuilder [^CodeBuilder xb ^Binding b ^Expr v
                           ^TypeDescriptor$OfField tp]
             (let [bref (.bref b)
                   slot (.allocateLocal xb (cf/type-kind tp))
                   ld-st (cf/ld-st-local tp (x/mnm bref) slot)]
               (.deliver-load bref ld-st)
               (-> xb (expr-insns es v) (cf/st-insn ld-st true)))))
          (mk-replay ^ILdStInstr [^Expr x]
            (reify ILdStInstr
              (emit-load [_ xb]
                (expr-insns xb es x))
              (emit-store [_ xb binding?]
                (throw (UnsupportedOperationException.)))
              (emit-iinc [_ xb iinc-delta]
                (throw (UnsupportedOperationException.)))
              (mh-if-global [_ l]
                (throw (UnsupportedOperationException.)))))
          (binding-insn ^CodeBuilder [^CodeBuilder xb ^Binding b]
            ;; Note: Several variants of binding may translate to an
            ;; empty instruction sequence, but `b` itself cannot be
            ;; treated as a regular empty instruction.
            (let [bref (.bref b)
                  init-x (x/first-arg x)]
              (case (x/b-kind bref)
                #_b-def 2r00000
                (when-not (x/alias-ref? bref)
                  (let [ld-st (x/deref-load (.bref b))]
                    (when (and (some? init-x)
                               (cf/ld-st-field? ld-st)
                               (not (x/constant-value-attribute? init-x)))
                      (-> (expr-insns xb es init-x)
                          (cf/st-insn ld-st true)))))
                
                #_b-let 2r01000
                (if (or (x/const-value? init-x)
                        (x/pure-let-initialization? init-x))
                  (.deliver-load bref (mk-replay init-x))
                  
                  (if-some [fn-form (x/root-fn-form init-x)]
                    (if-some [const (x/root-fn-const fn-form)]
                      (.deliver-load bref (cf/ld-st-constant const))
                      (let [tp (-> fn-form .!fnc .ld-clx)]
                        (bind-local-insn xb b init-x tp)))
                    (bind-local-insn xb b init-x)))

                #_b-loop 2r01100
                (bind-local-insn xb b init-x)))
            xb)

          (invoke-xref ^CodeBuilder [^CodeBuilder xb ^InvokeXref x]
            (let [mhd (.mhd x)]
              (-> (cond-> xb
                    (= (.kind mhd) rt/mhk-constructor)
                    (-> (.new_ (.owner mhd)) (.dup)))
                  (exprv-insns (x/argv x))
                  (line-number x)
                  (cf/invoke mhd))))
          (invoke-dynamic ^CodeBuilder [^CodeBuilder xb ^InvokeDynamic x]
            (let [dcsd (DynamicCallSiteDesc/of
                        (.bsm x) (.invocation-name x)
                        (rt/mtd-of (x/type x) (map x/type (x/argv x)))
                        ^"[Ljava.lang.constant.ConstantDesc;"
                        (->> (.cexprs x)
                             (map #(-> (x/recover-primitive-const %)
                                       (rt/describe-const-expr)))
                             (into-array ConstantDesc)))]
              (line-number xb x)
              (.invokedynamic xb dcsd)))
          (invoke-poly ^CodeBuilder [^CodeBuilder xb ^InvokePoly x]
            (let [argv (x/argv x)
                  mtd (rt/mtd-of (x/type x) (map x/type (rest argv)))] ;virtual
              (-> (exprv-insns xb argv)
                  (line-number x)
                  (.invokevirtual (.owner x) (.method-name x) mtd))))
          (invoke-arity-fa ^void [^CodeBuilder xb ^InvokeArity x ^IEmitPlan ep
                                  ^Arity at ^DirectMethodHandleDesc mhd]
            (if (.invoke-arity-with-self? ep)
              (exprv-insns xb (x/argv x)) ;no additional env parameters
              (do (.arity-env ep xb es at)
                  (exprv-insns xb (subvec (x/argv x) 1))))
            (line-number xb x)
            (cf/invoke xb mhd))
          (invoke-arity-va ^void [^CodeBuilder xb ^InvokeArity x ^IEmitPlan ep
                                  ^Arity at ^DirectMethodHandleDesc mhd]
            (.constantInstruction xb mhd)
            (let [atypes (if (.invoke-arity-with-self? ep)
                           (do (exprv-insns xb (x/argv x))
                               (map x/type (x/argv x)))
                           (let [tpv (.arity-env ep xb es at)
                                 argv (subvec (x/argv x) 1)]
                             (exprv-insns xb argv)
                             (into tpv (map x/type argv))))]
              (line-number xb x)
              (.invokevirtual xb rt/MethodHandle "invoke"
                              (rt/mtd-of (.rtype at) atypes))))
          (invoke-arity ^CodeBuilder [^CodeBuilder xb ^InvokeArity x]
            (let [fnc (-> x .fn-form .!fnc)
                  ac (.arity-code fnc (.at-idx x))
                  ep (.emit-plan fnc)]
              (if (x/at-extern-varargs? (.at ac))
                (invoke-arity-va xb x ep (.at ac) (.load-mh-ext ac))
                (invoke-arity-fa xb x ep (.at ac) (.load-mh-ext ac)))
              xb))

          (invoke-setter ^CodeBuilder [^CodeBuilder xb ^InvokeXref x]
            (let [mhd (.mhd x)
                  tpd (-> mhd .invocationType .returnType)]
              (line-number xb x)
              (if (= (.kind mhd) rt/mhk-getter)
                (.putfield xb (.owner mhd) (.methodName mhd) tpd)
                (.putstatic xb (.owner mhd) (.methodName mhd) tpd))))
          (setter ^CodeBuilder [^CodeBuilder xb ^Setter x]
            (let [v (x/first-arg x)]
              (cond
                (x/invoke-xref? v)
                (-> (exprv-insns xb (x/argv v)) ;getter argument, if any
                    (exprv-insns (subvec (x/argv x) 1)) ;value and maybe DUP*
                    (invoke-setter ^InvokeXref v))
                
                (x/bref? v)
                (let [bref ^BindingRef v]
                  (when (= x/b-field (x/b-kind bref)) (.aload xb 0))
                  (exprv-insns xb (subvec (x/argv x) 1)) ;value and maybe DUP*
                  (cf/st-insn xb (x/deref-load bref) false))
                
                :else (throw (IllegalStateException.)))
              xb))
          
          (branch-opcode! ^Opcode [^CodeBuilder xb ^Branch branch]
            (let [opc (.opcode branch)]
              (loop [opcd (.opcd branch)
                     left (x/nth-arg branch 0)
                     right (x/nth-arg branch 1)]
                ;; loop body is executed at most twice
                (cond
                  (x/const-value? right jb/jvm-int-zero)
                  (do (expr-insns xb es left)
                      (cf/bc2opc opc (- opcd 6))) ;slide if_icmpeq to ifeq
                  
                  (x/const-value? left jb/jvm-int-zero)
                  (recur (cf/swap-cmp opcd) right left)
                  
                  (x/const-value? right jb/jvm-null)
                  (do (expr-insns xb es left)
                      (cf/bc2opc opc (+ opcd 33))) ;slide if_acmpeq to ifnull
                  
                  (x/const-value? left jb/jvm-null)
                  (recur (cf/swap-cmp opcd) right left)
                  
                  :else
                  (do (-> xb (expr-insns es left) (expr-insns es right))
                      (cf/bc2opc opc opcd))))))
          (if-then-else ^CodeBuilder [^CodeBuilder xb ^Branch branch-to-true
                                      ^Expr if-true ^Expr if-false]
            (let [start-false (.newLabel xb), end-false (.newLabel xb)
                  opc (branch-opcode! xb (x/with-negate-cmp branch-to-true))]
              (-> (.branchInstruction xb opc start-false)
                  (expr-insns es if-true)
                  (cond-> #_xb
                    (x/returns? if-true)
                    (.branchInstruction Opcode/GOTO end-false))
                  (.labelBinding start-false)
                  (expr-insns es if-false)
                  (.labelBinding end-false))))
          (if-then ^CodeBuilder [^CodeBuilder xb ^Branch branch-to-true
                                 ^Expr if-true]
            (let [end-true (.newLabel xb)
                  opc (branch-opcode! xb (x/with-negate-cmp branch-to-true))]
              (-> (.branchInstruction xb opc end-true)
                  (expr-insns es if-true)
                  (.labelBinding end-true))))
          (split-join-insn ^CodeBuilder [^CodeBuilder xb ^Branch branch-to-true
                                         ^Expr if-true ^Expr if-false]
            (cond
              (x/expr-empty? if-false)
              (if-then xb branch-to-true if-true)
              (x/expr-empty? if-true)
              (if-then xb (x/with-negate-cmp branch-to-true) if-false)
              :else
              (if-then-else xb  branch-to-true if-true if-false)))
          (table-insn ^CodeBuilder [^CodeBuilder xb ^Switch x]
            (let [argv (x/argv x) ;first: switch expression; peek: default path
                  labelv (vec (repeatedly (dec (count argv)) #(.newLabel xb)))
                  sc-list (map (fn ^SwitchCase [^Guard g]
                                 (SwitchCase/of (.gint g)
                                                (nth labelv (.path-idx g))))
                               (.guardv x))]
              (loop [lo Integer/MAX_VALUE, hi Integer/MIN_VALUE
                     [^SwitchCase sc :as scs] (seq sc-list)]
                (if (nil? scs)
                  (let [dtarget (peek labelv)
                        n (count labelv)
                        end-switch (.newLabel xb)]
                    (expr-insns xb es (x/first-arg x))
                    (if (cf/table-switch? lo hi (count sc-list))
                      (.tableSwitchInstruction xb lo hi dtarget sc-list)
                      (.lookupSwitchInstruction xb dtarget sc-list))
                    (dotimes [i n]
                      (let [case-x (nth argv (inc i))]
                        (.labelBinding xb (nth labelv i))
                        (expr-insns xb es case-x)
                        (when (and (x/returns? case-x) (< i (dec n)))
                          (.goto_ xb end-switch)))) ;omit goto for default path
                    (.labelBinding xb end-switch))
                  (recur (min lo (.caseValue sc)) (max hi (.caseValue sc))
                         (next scs))))))
          (switch-insn ^CodeBuilder [^CodeBuilder xb ^Switch x]
            (let [argv (x/argv x), guardv (.guardv x)]
              (case (count guardv)
                0                    ;switch expression + default path
                (expr-insns xb es (xf/prog2-expr (nth argv 0) (nth argv 1)))
                
                1 ;switch expression + single guard value + default path
                (let [case-value (object (.gint ^Guard (nth guardv 0)))
                      if-x (-> (xf/icmp (nth argv 0) (x/const jb/int case-value)
                                        cf/eq)
                               (x/split&join (x/type x) (nth argv 1)
                                             (nth argv 2) (.adpt-paths x)))]
                  (expr-insns xb es if-x))

                #_else (table-insn xb x))))

          (try-catch* ^boolean [^CodeBuilder xb ^TryCatchFinally x
                                ^Label start-body ^Label f-label]
            (let [body-expr (x/first-arg x)
                  handlerv (.handlerv x)
                  n (count handlerv)
                  c-labelv (vec (repeatedly n #(.newLabel xb)))
                  end-body (.newLabel xb)]
              (assert (= (x/arg-count x) (+ n 2)))
              (expr-insns xb es body-expr)
              (when (x/returns? body-expr)
                (.branchInstruction xb Opcode/GOTO f-label))
              (.labelBinding xb end-body)
              ;; here holds: the body's bytecode region [start-body,
              ;; end-body[ is not empty, because it holds either
              ;; ATHROW, xRETURN, or the "GOTO f-label"
              (dotimes [i n]
                (let [handler ^TryHandler (nth handlerv i)]
                  (.exceptionCatch xb start-body end-body
                                   ^Label (nth c-labelv i)
                                   (rt/cd-of (.exception handler))))
                (let [catch-expr (x/nth-arg x (inc i))]
                  (-> (.labelBinding xb (nth c-labelv i))
                      (expr-insns es catch-expr)
                      (cond-> (x/returns? catch-expr)
                        (.branchInstruction Opcode/GOTO f-label)))))
              (boolean (some x/returns? (butlast (x/argv x))))))
          (try-catch-finally ^CodeBuilder [^CodeBuilder xb ^TryCatchFinally x]
            (let [finally-statm ^Expr (peek (x/argv x))
                  start-body (.newLabel xb)
                  f-label (.newLabel xb)
                  f-reachable? (-> (.labelBinding xb start-body)
                                   (try-catch* x start-body f-label))]
              ;; here holds: the try-catch's bytecode region
              ;; [start-body, end-tc[ is not empty, it holds
              ;; at least the try body (see above)
              (when-not (x/expr-empty? finally-statm)
                (let [end-tc (.newLabel xb)
                      ca-label (.newLabel xb)]
                  (-> (.labelBinding xb end-tc)
                      (.exceptionCatchAll start-body end-tc ca-label)
                      (.labelBinding ca-label)
                      (expr-insns es finally-statm)
                      (cond-> (x/returns? finally-statm) (.athrow)))))
              (cond-> xb
                f-reachable? (-> (.labelBinding f-label)
                                 (expr-insns es finally-statm)))))
          
          (loop-body-insn ^CodeBuilder [^CodeBuilder xb ^LoopBody x]
            (let [es' (with-recur-target es (.newLabel xb))]
              (-> (.labelBinding xb (.recur-target es'))
                  (expr-insns es' (x/first-arg x)))))
          (recur-store-insns ^CodeBuilder [^CodeBuilder xb bv argv]
            (letfn [(recur-store [^Binding b ^Expr x]
                      (let [rc (int-expr-counts (.bref b) x)]
                        (if (rc-iinc? rc)
                          (object (.incr rc)) ;zero means identity store
                          x)))
                    (store-insn ^void [^Binding b store]
                      (let [ld-st (x/deref-load (.bref b))]
                        (if (instance? Integer store)
                          (cf/st-iinc xb ld-st ^int store)
                          (cf/st-insn xb ld-st false))))]
              (let [stores (mapv recur-store bv argv)]
                ;; push all values that are not folded into an IINC
                ;; onto the operand stack, from left to right
                (exprv-insns xb (into [] (filter x/expr?) stores))
                ;; execute stores, from right to left
                (mapv store-insn (rseq bv) (reverse stores))
                xb)))
          
          (fn-create-insn ^CodeBuilder [^CodeBuilder xb ^FnForm fn-form]
            (.fn-value-instr (-> fn-form .!fnc .emit-plan) xb es)
            xb)
          (fn-patch-insn ^CodeBuilder [^CodeBuilder xb ^FnPatch fn-patch]
            (.fn-patch-instr (-> fn-patch .fn-form .!fnc .emit-plan)
                             xb es (.bref fn-patch))
            xb)
          
          (simple-insn ^CodeBuilder [^CodeBuilder xb ^Insn insn]
            (-> xb
                (exprv-insns (x/argv insn))
                (line-number insn)      ;primarily for ATHROW
                (.with (.instr insn))))
          (new-array-insn ^CodeBuilder [^CodeBuilder xb ^NewArray insn]
            (let [tp (x/type insn)
                  ln (count (x/argv insn))]
              (exprv-insns xb (x/argv insn))
              (if (= ln 1)
                (let [ctp (.componentType tp)]
                  (if (.isPrimitive ctp)
                    (.newarray xb (cf/type-kind ctp))
                    (.anewarray xb (rt/cd-of ctp))))
                (.multianewarray xb (rt/cd-of tp) ln))))]

    ;; At this point all translation decisions have already been made.
    ;; In particular, it is known how functions are emitted as methods
    ;; and how their arities are invoked.
    #_(prn :expr-insns (class x))
    (if-some [c (.const-value x)]
      (cond-> xb
        (not (x/expr-empty? x))
        (.constantInstruction (rt/describe-const-expr c)))
      (do
        ;; This line covers the arguments, which may overwrite it.
        ;; The instruction is emitted at the end and should repeat its
        ;; own line (especially the invoke opcodes).
        (line-number xb x)
        (condp instance? x ;tests sorted by counts from "make test-tcljc"
          BindingRef       ;ValueRef & AliasRef
          (binding-ref xb ^BindingRef x)
          InvokeArity
          (invoke-arity xb ^InvokeArity x)
          Insn
          (simple-insn xb ^Insn x)
          Binding
          (binding-insn xb ^Binding x)
          InvokeXref
          (invoke-xref xb ^InvokeXref x)
          LoopBody
          (loop-body-insn xb ^LoopBody x)
          Block
          (.block xb (exprv-handler (x/argv x)))
          SplitJoin
          (split-join-insn xb ^Branch (x/nth-arg x 0) (x/nth-arg x 1) (x/nth-arg x 2))
          Progn
          (exprv-insns xb (x/argv x))
          InvokePoly
          (invoke-poly xb ^InvokePoly x)
          InvokeDynamic
          (-> xb (argv-insns) (invoke-dynamic ^InvokeDynamic x))
          FnForm
          (-> xb (argv-insns) (fn-create-insn ^FnForm x))
          FnPatch
          (-> xb (argv-insns) (fn-patch-insn ^FnPatch x))
          Recur
          (-> (recur-store-insns xb (.bv ^Recur x) (x/argv x))
              (.branchInstruction Opcode/GOTO (.recur-target es)))
          NewArray
          (new-array-insn xb ^NewArray x)
          VarRef
          (cf/ld-insn xb (x/deref-load ^BindingRef (x/first-arg x)))
          Switch
          (switch-insn xb ^Switch x)
          TryCatchFinally
          (try-catch-finally xb ^TryCatchFinally x)
          Setter
          (setter xb ^Setter x)
          AssignDef
          (-> (expr-insns xb es (x/first-arg x))
              (cf/st-insn (-> ^AssignDef x .bref (x/deref-load)) false))
          InvokeArrayClone
          (let [cld (rt/cd-of (x/type x))]
            (-> (argv-insns xb)
                (.invokevirtual cld "clone" rt/mtd-object-void)
                (.checkcast cld)))
          #_else (throw (IllegalArgumentException. (str (class x)))))))))

(defn emit-body
  (^void [^CodeBuilder xb ^Expr x]
   (let [es (EmitState. x/lvl-environment nil nil nil)]
     (expr-insns xb es (xc/as-returning-code x))))
  (^void [^CodeBuilder xb ^ILdStInstr ld-this ^Arity at
          ^int first-param-no ^Expr body-expr]
   (loop [i 0]
     (when-not (= i (count (.paramv at)))
       (let [bref (.bref ^Binding (nth (.paramv at) i))
             ld-st (cf/ld-st-parameter (x/type bref) (x/mnm bref)
                                       (+ i first-param-no))]
         (cf/st-insn xb ld-st true)      ;register variable name
         (.deliver-load bref ld-st))
       (recur (inc i))))
   (let [es (EmitState. (.level at) (.!closes-over at) ld-this nil)]
     (line-number xb body-expr)
     (expr-insns xb es (xc/as-returning-code body-expr)))))

(defn emit-global-b ^void [^ClassBuilder cb ^ClassDesc segm-cld ^Binding b]
  ;; To be able to do INVOKEVIRTUAL calls on global function defs, the
  ;; field/method return type must be the exact type of the function
  ;; instance.  It does not work if it's the abstract base type that
  ;; is used by the parser for function values.
  (letfn [(macro-annotation ^void [^ClassfileBuilder b ^BindingRef bref]
            (when (x/b-macro? bref)
              (let [ae (if-some [macro-v ^BindingRef (.const-value bref)]
                         (doto (new "[Ljdk.classfile.AnnotationElement;" 1)
                           (aset 0 (AnnotationElement/ofString
                                    "macroValue" (str (x/qname macro-v)))))
                         (new "[Ljdk.classfile.AnnotationElement;" 0))
                    ann (doto (new "[Ljdk.classfile.Annotation;" 1)
                          (aset 0 (Annotation/of rt/Macro ae)))]
                (.with b (RuntimeVisibleAnnotationsAttribute/of ann)))))
          (static-flags ^int [^BindingRef bref]
            (if (x/b-private? bref)
              Classfile/ACC_STATIC
              (bit-or Classfile/ACC_STATIC Classfile/ACC_PUBLIC)))
          (field-flags ^int [^BindingRef bref ^Expr init-x]
            (letfn [(init-with-const? ^boolean []
                      (or (some? (x/root-fn-const init-x))
                          (and (some? init-x) (x/const-value? init-x))
                          (x/alias-ref? bref)))]
              (bit-or (static-flags bref)
                      (cond
                        (x/b-mutable? bref) Classfile/ACC_VOLATILE
                        (init-with-const?) Classfile/ACC_FINAL
                        :else (bit-or Classfile/ACC_FINAL
                                      Classfile/ACC_TRANSIENT)))))
          (static-field! ^void [^BindingRef bref ^Expr init-x]
            (let [member-tp (cf/member-tp ^LdStMember (x/deref-load bref))
                  mnm (x/mnm bref)
                  fbh (cf/with-fbh [fb]
                        (macro-annotation fb bref)
                        (cond-> (.withFlags fb (field-flags bref init-x))
                          (x/b-def-var? bref)
                          ^FieldBuilder
                          (.with (-> (str "Lclojure/lang/Var<"
                                          (.descriptorString (x/type bref))
                                          ">;")
                                     (Signature/parseFrom)
                                     (SignatureAttribute/of)))
                          
                          (and (some? init-x)
                               (x/constant-value-attribute? init-x))
                          ^FieldBuilder
                          (.with (ConstantValueAttribute/of
                                  ^ConstantDesc (.const-value init-x)))))]
              (.withField cb mnm member-tp fbh)))
          (alias-annotation ^"[Ljdk.classfile.Annotation;" [^AliasRef bref]
            (let [qnm (-> (.alias-for bref) (x/qname) (str))
                  ae (doto (new "[Ljdk.classfile.AnnotationElement;" 1)
                       (aset 0 (AnnotationElement/ofString "of" qnm)))]
              (doto (new "[Ljdk.classfile.Annotation;" 1)
                (aset 0 (Annotation/of rt/Alias ae)))))
          (alias-field! ^void [^AliasRef bref]
            (let [member-tp rt/object
                  mnm (x/mnm bref)
                  fbh (cf/with-fbh [fb]
                        (-> fb
                            (.withFlags (bit-or (static-flags bref)
                                                Classfile/ACC_VOLATILE))
                            (.with (RuntimeVisibleAnnotationsAttribute/of
                                    (alias-annotation bref)))))]
              (.withField cb mnm member-tp fbh)))
          (lazy-const! ^void [^BindingRef bref ^TypeDescriptor$OfField member-tp
                              const]
            (let [mnm (x/mnm bref)
                  mtd (rt/mtd-of member-tp [])
                  xbh (cf/with-xbh [xb]
                        (-> (.constantInstruction xb ^ConstantDesc const)
                            (.areturn)))
                  mbh (cf/with-mbh [mb]
                        (macro-annotation mb bref)
                        (.withCode mb xbh))
                  flags (bit-or (static-flags bref) Classfile/ACC_FINAL)]
              (.withMethod cb mnm mtd flags mbh)))]

    (let [init-x (x/first-arg b)
          bref (.bref b)]
      (if (x/alias-ref? bref)
        (alias-field! ^AliasRef bref)
        
        (let [ld-st (x/deref-load bref)]
          (if (cf/ld-st-field? ld-st)
            (static-field! bref init-x)
            
            (let [tpd (cf/member-tp ^LdStMember ld-st)
                  const (or (x/root-fn-const init-x)
                            (rt/describe-const-expr (.const-value init-x)))]
              (lazy-const! bref tpd const))))))))
