;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.javabase     ;supporting code for stuff from java.base & JVM
  (:require
   [tcljc.config :as cfg])
  (:import
   (clojure.lang Symbol))
  (:refer-clojure
   :exclude [int long float double boolean char byte short void]))

;;; primitive types
(def void Void/TYPE)
(def int Integer/TYPE)
(def long Long/TYPE)
(def float Float/TYPE)
(def double Double/TYPE)
;;; the other "computational int" types
(def boolean Boolean/TYPE)
(def char Character/TYPE)
(def byte Byte/TYPE)
(def short Short/TYPE)

;;; ------------------------------------------------------------------------

(defn same-class? ^boolean [^Class a ^Class b]
  (identical? a b))
(defn same-classname? ^boolean [^Symbol clnm ^Class a]
  (= (name clnm) (.getName a)))

;; Note: `void` is also considered a primitive.  Result is undefined
;; for `no-return`.
(defn primitive? ^boolean [^Class tp]
  (.isPrimitive tp))
(defn primitive-operand? ^boolean [^Class tp]
  (and (.isPrimitive tp) (not (identical? void tp))))

(defn void? ^boolean [^Class tp]
  (same-class? void tp))

(defn character? ^boolean [^Class tp]
  (same-class? char tp))


;; Note: `cl-null` is considered a reference.  Result is undefined for
;; `no-return`.
(defn reference? ^boolean [^Class tp]
  (not (primitive? tp)))

(defn array? ^boolean [^Class tp]
  (.isArray tp))

(defn interface? ^boolean [^Class tp]
  (.isInterface tp))


(defn jvm-assignable-to? ^boolean [^Class a ^Class b]
  ;; Technically we use the more restrictive Java point of view here.
  ;; For type checking, the JVM itself treats an interface class
  ;; similar to java.lang.Object.
  (.isAssignableFrom b a))
(defn jvm-castable-to? ^boolean [^Class a ^Class b]
  (.isAssignableFrom a b))

;;; ------------------------------------------------------------------------

(def jvm-null java.lang.constant.ConstantDescs/NULL)
(def jvm-int-zero (Integer/valueOf 0))
(def jvm-int-one (Integer/valueOf 1))
(def jvm-float-one (Float/valueOf 1))

(defn in-short? ^boolean [^int n]
  (= n (tinyclj.core/short n)))

(def Object java.lang.Object)
(def ObjectArray java.lang.Object*)
(def String java.lang.String)
(def Class java.lang.Class)
(def Pattern java.util.regex.Pattern)
(def Exception java.lang.Exception)

(defn object? ^boolean [^Class tp]      ;exact match
  (same-class? Object tp))

;;; ------------------------------------------------------------------------

;;; This type is used to mark macro and primitive function values.
(def ^Class cl-compile-time-entity nil)

(defn compile-time-entity? ^boolean [^Class cl]
  (identical? cl-compile-time-entity cl))


;;; Special type indicating that the source or destination of an
;;; operand can be of arbitrary type.
;;;
;;; For the destination of an operand (parameter, return value, or
;;; field) this type indicates that the destination's type is that of
;;; the operand.
;;;
;;; As the return type of an expression, this type is short lived.  If
;;; some path of `compile-form` translates a form into such an
;;; expression, then the type tag processing at the end of
;;; `compile-form` replaces it with a proper class.
;;;
;;; Another short lived use is as marker of the still unknown return
;;; type of an `auto-return-type arity`.  This is an implementation
;;; detail private to `fn*-form`, `letfn*-form`, and `call-fn-def`.
;;;
;;; Finally, this type is used for `_` entries of :param-tags.
(deftype PolymorphicType [])
(def cl-polymorphic-type PolymorphicType)

(defn polymorphic? ^boolean [^Class cl]
  (identical? cl-polymorphic-type cl))


(defn throwable? ^boolean [^Class tp]   ;Throwable or subclass of it
  (.isAssignableFrom Throwable tp))

;;; ------------------------------------------------------------------------

(defn import-symbol-short ^Symbol [^Class cl]
  (let [s (.getName cl), i (.lastIndexOf s (tinyclj.core/int \.))]
    (symbol nil (.substring s (inc i)))))

(def jvm-imports
  (let [m (java.util.HashMap.)]
    ;; by default, import certain java.lang.* classes using their
    ;; short name; references using the long name are resolved on
    ;; demand and refer to the same JVM-level class instance
    (doseq [^Class cl cfg/java-lang-classes]
      (.put m (import-symbol-short cl) cl))
    
    ;; pseudo-import of primitive types types by their lower-case Java
    ;; name
    (doseq [^Class cl [void boolean byte short char int float long double]]
      (.put m (symbol nil (.getSimpleName cl)) cl))
    (into {} m)))

(defn computational-const ^Object [^Object x]
  (cond
    (nil? x) jvm-null
    (true? x) jvm-int-one
    (false? x) jvm-int-zero
    (instance? Character x) (object (tinyclj.core/int (.charValue ^Character x)))
    (instance? Byte x) (object (.intValue ^Byte x))
    (instance? Short x) (object (.intValue ^Short x))
    :else x))

(defn int-value-of ^int [^Object x]     ;pre: `x` is computational int
  (cond (instance? Number x) (.intValue ^Number x)
        (instance? Character x) (tinyclj.core/int (.charValue ^Character x))
        (instance? Boolean x) (if ^boolean x 1 0)
        :else (throw (IllegalArgumentException. (str (class x))))))
