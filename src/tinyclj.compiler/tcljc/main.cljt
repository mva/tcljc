;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.main
  (:require [tcljc.config :as cfg]
            [tcljc.main.options :as options]
            [tcljc.main.invoke :as invoke]
            [tcljc.main.tclj0 :as tclj0]
            [tcljc.main.tclj1 :as tclj1])
  (:import (tcljc.main.options Options)
           (java.util Optional)
           (java.util.function Consumer)))

(defn print-exception-info [^clojure.lang.ExceptionInfo e]
  (println "ExceptionInfo:" (ex-message e))
  (doseq [[k v] (sort-by key (ex-data e))]
    (println k (if (= k :form/context) (last v) v))))

(defn -main [& args]
  (letfn [(coded-exit ^void [^boolean success]
            #_(let [t (-> (^Runnable fn []
                           nil)
                          (Thread/startVirtualThread))]
                (prn :final-virtual-thread-id (.threadId t))
                (.join t))
            #_(when (seq (System/getenv "TCLJ0_REPEAT"))
                (println "jcmd" (.pid java.lang.ProcessHandle/current) "JFR.view" "hot-methods")
                (let [seconds 60]
                  (println "Sleeping for" seconds "seconds...")
                  (Thread/sleep (* seconds 1000))))
            (System/exit (if success 0 1)))
          (usage ^boolean []
            (println (str "usage: java ... [--watch] [-d <dest-dir] "
                          "[-s <classpath>]... <main-ns>/<run>..."))
            false)]
    (-> (try
          (if (contains? #{nil, "--help", "-h"} (first args))
            (usage)
            (if-some [opts (options/parse args)]
              (do #_(println "jcmd" (.pid java.lang.ProcessHandle/current)
                             "Thread.dump_to_file" "-format=json"
                             "/tmp/threads.json")
                  (if (.watch-mode? opts)
                    (tclj1/run opts)
                    (do #_(dotimes [_ (if-some [repeat (System/getenv "TCLJ0_REPEAT")]
                                        (try
                                          (dec (Integer/parseInt repeat))
                                          (catch NumberFormatException _
                                            0))
                                        0)]
                            (tclj0/run opts))
                        (tclj0/run opts))))
              false))
          (catch clojure.lang.ExceptionInfo e
            (print-exception-info e)
            false)
          (catch Throwable t
            (.printStackTrace t)
            false)            ;failure because of unhandled expception
          (finally
            (shutdown-agents))) ;can only shut down when owning the JVM
        (coded-exit))))           ;can only exit when owning the JVM

;;; Start watch/compile/report loop.  After each build attempt,
;;; `value-consumer` is called with an argument of type Optional.  On
;;; success it holds the value of the namespace var to which
;;; `var-name` resolves.  On failure it is empty.  This function never
;;; returns normally.
(defn start
  ^void [^String dest-dir               ;nil means default directory
         ^clojure.lang.IPersistentVector sourcev
         ^clojure.lang.Symbol var-name  ;namespace qualified
         ^java.util.function.Consumer value-consumer]
  (when (nil? (namespace var-name))
    (options/abort (str "error: var name `" var-name
                        "` must be a namespace qualified symbol")))
  (doseq [x sourcev]
    (when-not (instance? java.nio.file.Path x)
      (options/abort (str "error: sourcev entry must java.nio.file.Path"))))
  (let [opts (Options. (options/to-dest-dir dest-dir) ;dest-dir
                       :system               ;parent-classloader
                       sourcev               ;sourcev
                       (list (str var-name)) ;targets
                       false                 ;deterministic?
                       15000                 ;wait-cyclic-ms
                       true                  ;watch-mode?
                       20)                   ;wait-after-change-ms
        on-completion (^Consumer fn [opt-nmsp-map]
                       (.accept value-consumer
                                (if (.isEmpty ^Optional opt-nmsp-map)
                                  (Optional/empty)
                                  (let [nmsp-map (.get ^Optional opt-nmsp-map)]
                                    (-> (invoke/namespace-of nmsp-map var-name)
                                        (invoke/var-value var-name)
                                        (Optional/of))))))]
    (tclj1/run opts on-completion)))
