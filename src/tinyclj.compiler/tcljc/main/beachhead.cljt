;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.main.beachhead
  (:require [tcljc.classfile :as cf])
  (:import (java.lang.classfile ClassFile)
           (java.lang.constant ClassDesc MethodTypeDesc ConstantDescs)
           (java.lang.invoke MethodHandles$Lookup MethodType)))

(defn- beachhead ^byte* [^String pkg-name]
  (.build
   (ClassFile/of) (ClassDesc/of pkg-name "__beachhead")
   (cf/with-cbh [cb]
     (let [mtd (MethodTypeDesc/of ConstantDescs/CD_MethodHandles_Lookup)]
       (-> (cf/with-version cb)
           (.withFlags ClassFile/ACC_PUBLIC)
           (.withMethodBody
            "beachheadLookup" mtd
            (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_STATIC)
            (cf/with-xbh [xb]
              (-> (.invokestatic xb ConstantDescs/CD_MethodHandles "lookup" mtd)
                  (.areturn)))))))))

(defn mk-lookup-in-pkg ^MethodHandles$Lookup [^MethodHandles$Lookup rt-lookup
                                              ^ClassLoader dyn-ld
                                              ^String pkg-name]
  (let [bs (beachhead pkg-name)
        mt (MethodType/methodType MethodHandles$Lookup byte*)
        mh (.findVirtual rt-lookup (class dyn-ld) "lookupInPackage" mt)]
    ^MethodHandles$Lookup (.invokeExact (.bindTo mh dyn-ld) bs)))
