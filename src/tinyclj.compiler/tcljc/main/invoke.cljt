;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.main.invoke
  (:require
   [tcljc.wrong :as wrong]
   [tcljc.javabase :as jb]
   [tcljc.runtime :as rt]
   [tcljc.context :as ctx]
   [tcljc.expr :as x]
   tcljc.emitter.namespace
   [tcljc.io :as io]
   [tcljc.main.options :as options]
   [tinyclj.alpha.ptest__term :as term])
  (:import
   (tcljc.expr ValueRef)
   (tcljc.emitter.namespace Namespace)
   (tcljc.main.options Options)
   (clojure.lang Symbol Keyword IPersistentMap)
   (java.lang.invoke MethodHandle MethodHandles MethodHandles$Lookup)))

(defn namespace-of ^Namespace [^IPersistentMap nmsp-map ^Symbol var-name]
  (or (-> (get nmsp-map (namespace var-name))
          ^Namespace (io/get-result-now))
      (wrong/other (str "[namespace] no such namespace: "
                        (wrong/q (namespace var-name))))))

(defn- nmsp-lookup ^MethodHandles$Lookup [^Namespace nmsp]
  (.in (MethodHandles/publicLookup) (.capstone nmsp)))

(defn var-value
  (^Object [^Namespace nmsp ^Symbol var-name]
   (var-value (nmsp-lookup nmsp) nmsp var-name))
  (^Object [^MethodHandles$Lookup l ^Namespace nmsp ^Symbol var-name]
   (if-some [bref (-> (.exported-known-ns nmsp)
                      (get (namespace var-name))
                      ^ValueRef (get (name var-name)))]
     (let [member-mh (.mh-if-global (x/deref-load bref) l)]
       (.invoke member-mh))
     (wrong/other (str "[var-value] no public var " (wrong/q var-name))))))

(defn- applicable ^MethodHandle [^IPersistentMap nmsp-map ^Symbol fn-spec]
  (let [nmsp (namespace-of nmsp-map fn-spec)
        l (nmsp-lookup nmsp)
        value (var-value l nmsp fn-spec)]
    (.bindTo (.resolveConstantDesc rt/mhd-apply l) value)))

(defn- result-style [^Keyword res]
  (case res
    (:error :fail) :error
    :success :success
    :other :other
    nil :no-result))

(defn invoke-until-failure [^IPersistentMap nmsp-map ^Options opts
                            ^boolean print-var?]
  (letfn [(target-invoke-fnv [^Options opts]
            (letfn [(target-invoke-fn? ^boolean [^String s]
                      ;; note: don't produce empty namespace for "/..."
                      (not (neg? (.indexOf s (int \/) 1))))]
              (->> (filter target-invoke-fn? (.targets opts))
                   (mapv symbol))))
          (encode-result ^Keyword [res]
            (if (and (some? res)
                     (jb/same-classname? rt/clnm-keyword (.getClass res)))
              (case (str res)
                ":error" :error         ;non-zero error count
                ":fail" :fail           ;non-zero fail count
                ":success" :success
                #_else :other)          ;none of the above
              :other))]
    (let [fn-specv (target-invoke-fnv opts)
          n (count fn-specv)
          applicablev (mapv #(applicable nmsp-map %) fn-specv)
          resultv (vec (repeat n nil))]
      (loop [acc (vec (repeat n nil)), i 0]
        (if (= i n)
          acc
          (let [res (try
                      (when print-var?
                        (println)
                        (println (term/highlight "###")
                                 (str "calling " (nth fn-specv i))))
                      (-> (.invoke ^MethodHandle (nth applicablev i) nil)
                          (encode-result))
                      (catch Throwable t
                        (.printStackTrace t)
                        :error))
                acc (assoc acc (object i) res)]
            (if (identical? :error (result-style res))
              acc
              (recur acc (inc i)))))))))

(defn- invokes-successful? ^boolean [resultv]
  (or (nil? (seq resultv))
      (not (identical? :error (result-style (peek resultv))))))

(defn invoke-and-report ^boolean [^IPersistentMap nmsp-map ^Options opts]
  (let [resultv (invoke-until-failure nmsp-map opts false)]
    (invokes-successful? resultv)))

(defn completion-report ^String [^Options opts resultv]
  (letfn [(style ^String [^String s res]
            (case (result-style res)
              :error (term/error s)
              :success (term/success s)
              :other (term/highlight s)
              nil (term/dim s)))]
    (loop [acc (str (if (invokes-successful? resultv)
                      (term/success "###")
                      (term/error "###"))
                    " Completed:")
           [^String t :as targets] (seq (.targets opts))
           i 0]
      (if (nil? targets)
        acc
        (let [ns-str (options/target-ns-str t)]
          (if (options/target-invoke-fn? t)
            (recur (str acc " " (term/highlight ns-str)
                        (style (.substring t (.length ns-str)) (nth resultv i)))
                   (next targets)
                   (inc i))
            (recur (str acc " " (term/highlight ns-str))
                   (next targets)
                   i)))))))
