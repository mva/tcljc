;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljc.synquote
  (:require [tcljc.config :as cfg]
            [tcljc.expr :as x]
            [tcljc.context :as ctx]
            [tinyclj.string :as str])
  (:import (tcljc.expr BindingRef)
           (tcljc.context Ctx)
           (clojure.lang Symbol IPersistentVector IMapEntry IRecord)))

;;; note: a copy of this lives in tinyclj.core/specials
(def specials '#{def loop* recur if case* let* letfn*
                 do fn* quote var import* . set! deftype* reify* try throw
                 monitor-enter monitor-exit catch finally new &
                 ;; special forms that are not special in Clojure:
                 definterface* instanceof*})

(def ^:private lacks-namespace "`") ;marks simple symbol inside syntax quote
(def sym-unquote (symbol "," "unquote")) ;unreadable symbol
(def sym-unquote-splicing (symbol "," "unquote-splicing")) ;unreadable symbol

(defn unquote? ^boolean [form]
  (and (seq? form) (= sym-unquote (first form))))
(defn unquote-splicing? ^boolean [form]
  (and (seq? form) (= sym-unquote-splicing (first form))))

(defn mk-gensym-map []
  (atom {}))

(defn- replacement-symbol [current-replacement to-be-replaced-sym]
  (or current-replacement ;keep entry that has been added concurrently
      (let [nm (name to-be-replaced-sym)]
        (symbol nil (-> (.substring nm 0 (dec (.length nm)))
                        (str  "__" (clojure.lang.RT/nextID) "__auto__"))))))

;;; Assume the compiler's `clojure.lang.RT/nextID` can be used here,
;;; instead of the application's one.  Most of the time the two are
;;; identical -- unless the `:platform` class loader is used, aka the
;;; bootstrap scenario.  If they are distinct, the generated name
;;; cannot conflict with an application `gensym`, because the latter
;;; currently does not support a `suffix-string` like the "__auto__"
;;; above.  Note: There is the option to use per-input atomic counters
;;; starting at a defined value, but this would still leave make macro
;;; use of `gensym` non-deterministic.
(defn wrap-synquote-name ^Symbol [^Symbol sym agensym-map]
  (if (some? (namespace sym))
    sym
    (if (str/ends-with? (name sym) "#")
      (or (get @agensym-map sym)
          (get (swap! agensym-map update sym replacement-symbol sym) sym))
      (if (contains? specials sym)
        sym
        ;; mark symbol for further processing by synquote/encode-symbol
        (symbol lacks-namespace (name sym))))))

;;; ------------------------------------------------------------------------

(defn- encode-symbol [sym ^Ctx ctx]
  (letfn [(resolve-class-name ^String [^String nm]
            (if-some [tp (ctx/lookup-type (symbol nil nm) ctx)]
              (.getName tp)
              (when-not (neg? (.indexOf nm (int \.)))
                nm)))
          (trim-last ^String [^String nm]
            (.substring nm 0 (dec (.length nm))))]
    (let [nm (name sym)
          ns-part (namespace sym)]
      (if (= lacks-namespace ns-part) ;simple symbol inside syntax quote?
        (cond
          (.startsWith nm ".")          ;method name and ..
          (symbol nil nm)
            
          (.endsWith nm ".")            ;shorthand for (new ...)
          (if-some [nm' (resolve-class-name (trim-last nm))]
            (symbol nil (str nm' "."))
            (symbol nil nm))
            
          :else
          (if-some [bref ^BindingRef (.valAt (.get-globals ctx) nm)]
            (x/qname bref)
            (if-some [nm' (resolve-class-name nm)]
              (symbol nil nm')
              (symbol (.ns-str ctx) nm))))
        
        (if-some [ns-part' (and ns-part
                                (or (.valAt (.get-ns-alias-map ctx) ns-part nil)
                                    (resolve-class-name ns-part)))]
          (symbol ns-part' nm)
          sym)))))

;; Baseline is the method syntaxQuote() from Clojure's
;; LispReader.java.

(defn encode [form ^Ctx ctx]
  (letfn
      [(wrong [^String msg]
         (throw (ex-info msg {}))) ;no line/column info available here
       (q [form]
         (list cfg/sym-quote form))
            
       (expand-list ^IPersistentVector [xs]
         (reduce (fn ^IPersistentVector [^IPersistentVector ret item]
                   (.cons ret (cond
                                (unquote? item) (list `list (second item))
                                (unquote-splicing? item) (second item)
                                :else (list `list (encode item ctx)))))
                 [] xs))
       (flatten-map ^IPersistentVector [m]
         (reduce (fn ^IPersistentVector [^IPersistentVector keyvals
                                         ^IMapEntry e]
                   (-> keyvals (.cons (.key e)) (.cons (.val e))))
                 [] (seq m)))

       (quoted? ^boolean [x]        ;quoted form or autoquoted literal
         ;; Assumption: values with non-constant meta are represented
         ;; by a (with-meta ...) form in `x` and return false.
         (if (seq? x)
           (and (= cfg/sym-quote (first x)) (nil? (meta x)))
           (not (symbol? x))))
       (unwrap-quote [x]                ;pre: `(quoted? x)` is true
         (if (seq? x) (second x) x))
       (quoted-in-list? ^boolean [x]
         (and (seq? x) (= `list (first x)) (nil? (nnext x)) ;list of one?
              (quoted? (second x))))
       (unwrap-list [x]            ;pre: `(quoted-in-list? x)` is true
         (unwrap-quote (second x)))
       (list-of? ^boolean [l]
         (and (seq? l) (= (first l) `list)))
       (concat-of [ls]
         (if (every? list-of? ls)
           (list* `list (mapcat rest ls))
           (cons `concat ls)))
       (assemble [apply-sym apply-fn xs]
         (let [ls (expand-list xs)]
           (if (every? quoted-in-list? ls)
             (q (cond->> (map unwrap-list ls)
                  (some? apply-fn) (apply apply-fn)))
             (cond->> (list `seq (concat-of ls))
               (some? apply-sym) (list `apply apply-sym)))))
       
       (encode-collection [form]
         (cond
           (instance? IRecord form) form
           (map? form) (assemble `hash-map hash-map (flatten-map form))
           (vector? form) (assemble `vector vector form)
           (set? form) (assemble `hash-set hash-set form)
           (or (seq? form) (list? form)) (if-some [seq (seq form)]
                                           (assemble nil nil seq)
                                           ())
           :else (throw (UnsupportedOperationException.
                         "unknown collection type"))))]

      (let [tmpl (cond
                   (contains? specials form)
                   (q form)

                   (symbol? form)
                   (q (encode-symbol form ctx))

                   (unquote? form)
                   (second form)

                   (unquote-splicing? form)
                   (wrong "unquote-splicing ~@ outside of list")

                   (coll? form)
                   (encode-collection form)

                   ;; all other forms produced by the reader are
                   ;; auto-quoting
                   :else form)]
        (if-some [m (cfg/source-meta form)]
          (let [m-tmpl (encode m ctx)]
            (if (and (quoted? tmpl) (quoted? m-tmpl))
              (q (with-meta (unwrap-quote tmpl) (unwrap-quote m-tmpl)))
              (list `with-meta tmpl m-tmpl)))
          tmpl))))
